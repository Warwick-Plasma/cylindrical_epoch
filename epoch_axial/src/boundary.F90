! Copyright (C) 2009-2019 University of Warwick
!
! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.

MODULE boundary

  USE partlist
  USE particle_temperature
  USE laser
  USE mpi_subtype_control
  USE utilities
  USE particle_id_hash_mod
  USE injectors

  IMPLICIT NONE

CONTAINS

  SUBROUTINE setup_boundaries

    INTEGER :: i, ispecies, bc
    LOGICAL :: error
    CHARACTER(LEN=5), DIMENSION(2*c_ndims) :: &
        boundary = (/ 'x_min', 'x_max', 'y_min', 'y_max' /)
    CHARACTER(LEN=2*c_max_string_length) :: bc_error

    ! For some types of boundary, fields and particles are treated in
    ! different ways, deal with that here

    add_laser(:) = .FALSE.
    any_open = .FALSE.
    cpml_boundaries = .FALSE.
    DO i = 1, 2*c_ndims
      IF (bc_field(i) == c_bc_other) bc_field(i) = c_bc_clamp
      IF (bc_field(i) == c_bc_cpml_laser &
          .OR. bc_field(i) == c_bc_cpml_outflow) cpml_boundaries = .TRUE.
      IF (bc_field(i) == c_bc_simple_laser) THEN
        add_laser(i) = .TRUE.
        any_open = .TRUE.
      END IF

      ! Note: reflecting EM boundaries not yet implemented.
      IF (bc_field(i) == c_bc_reflect) bc_field(i) = c_bc_clamp
      IF (bc_field(i) == c_bc_open) bc_field(i) = c_bc_simple_outflow
      IF (bc_field(i) == c_bc_simple_outflow) any_open = .TRUE.
    END DO

    error = .FALSE.
    DO ispecies = 1, n_species
      DO i = 1, 2*c_ndims
        IF (i==3) CYCLE ! No r=0 boundary condition for cylindrical co-ords
        bc = species_list(ispecies)%bc_particle(i)
        bc_error = 'Unrecognised "' // TRIM(boundary(i)) // '" boundary for ' &
            // 'species "' // TRIM(species_list(ispecies)%name) // '"'
        error = error .OR. setup_particle_boundary(bc, bc_error)
      END DO
    END DO

    IF (error) THEN
      errcode = c_err_bad_value
      CALL abort_code(errcode)
    END IF

  END SUBROUTINE setup_boundaries



  SUBROUTINE setup_domain_dependent_boundaries

    ! Any boundary condition setup that needs the domain to have already been
    ! created should be added here

    INTEGER :: ispecies, i, bc

    DO ispecies = 1, n_species
      DO i = 1, 2*c_ndims
        bc = species_list(ispecies)%bc_particle(i)
        IF (bc == c_bc_heat_bath) THEN
          CALL create_boundary_injector(ispecies, i)
          species_list(ispecies)%bc_particle(i) = c_bc_open
        END IF
      END DO
    END DO

  END SUBROUTINE setup_domain_dependent_boundaries



  FUNCTION setup_particle_boundary(boundary, bc_error) RESULT(error)

    INTEGER, INTENT(INOUT) :: boundary
    CHARACTER(LEN=*), INTENT(IN) :: bc_error
    LOGICAL :: error

    ! For some types of boundary, fields and particles are treated in
    ! different ways, deal with that here

    IF (boundary == c_bc_other .OR. boundary == c_bc_conduct) &
        boundary = c_bc_reflect

    ! Note, for laser bcs to work, the main bcs must be set IN THE CODE to
    ! simple_laser (or outflow) and the field bcs to c_bc_clamp. Particles
    ! can then be set separately. IN THE DECK, laser bcs are chosen either
    ! by seting the main bcs OR by setting the field bcs to simple_laser
    ! (or outflow).

    ! Laser boundaries assume open particles unless otherwise specified.
    IF (boundary == c_bc_simple_laser &
        .OR. boundary == c_bc_simple_outflow &
        .OR. boundary == c_bc_cpml_laser &
        .OR. boundary == c_bc_cpml_outflow) &
            boundary = c_bc_open

    ! Sanity check on particle boundaries
    error = .FALSE.
    IF (boundary == c_bc_periodic &
        .OR. boundary == c_bc_reflect &
        .OR. boundary == c_bc_thermal &
        .OR. boundary == c_bc_heat_bath &
        .OR. boundary == c_bc_open) RETURN

    IF (rank == 0) THEN
      WRITE(*,*)
      WRITE(*,*) '*** ERROR ***'
      WRITE(*,*) TRIM(bc_error)
    END IF
    error = .TRUE.

  END FUNCTION setup_particle_boundary



  ! Exchanges field values at processor boundaries and applies field
  ! boundary conditions
  SUBROUTINE field_bc(field, ng)

    INTEGER, INTENT(IN) :: ng
    REAL(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: field

    CALL do_field_mpi_with_lengths(field, ng, nx, ny)

  END SUBROUTINE field_bc



  ! Exchanges field values at processor boundaries for field arrays with modes
  SUBROUTINE field_mode_bc(field, ng)

    INTEGER, INTENT(IN) :: ng
    COMPLEX(num), DIMENSION(1-ng:,1-ng:,0:), INTENT(INOUT) :: field
    INTEGER :: im

    DO im = 0,n_mode-1
      CALL do_field_mpi_with_lengths_cmplx(field(1-ng:nx+ng,1-ng:ny+ng,im), &
          ng, nx, ny)
    END DO

  END SUBROUTINE field_mode_bc



  SUBROUTINE do_field_mpi_with_lengths_slice(field, direction, ng, n1_local)

    INTEGER, INTENT(IN) :: direction, ng
    REAL(num), DIMENSION(1-ng:), INTENT(INOUT) :: field
    INTEGER, INTENT(IN) :: n1_local
    INTEGER :: proc1_min, proc1_max
    INTEGER :: basetype, i, n
    REAL(num), ALLOCATABLE :: temp(:)

    basetype = mpireal

    IF (direction == c_dir_x) THEN
      IF (.NOT. y_min_boundary .OR. bc_field(c_bd_y_min) == c_bc_periodic) THEN
        proc1_min = proc_y_min
      ELSE
        proc1_min = MPI_PROC_NULL
      END IF

      IF (.NOT. y_max_boundary .OR. bc_field(c_bd_y_max) == c_bc_periodic) THEN
        proc1_max = proc_y_max
      ELSE
        proc1_max = MPI_PROC_NULL
      END IF
    ELSE
      IF (.NOT. x_min_boundary .OR. bc_field(c_bd_x_min) == c_bc_periodic) THEN
        proc1_min = proc_x_min
      ELSE
        proc1_min = MPI_PROC_NULL
      END IF
      IF (.NOT. x_max_boundary .OR. bc_field(c_bd_x_max) == c_bc_periodic) THEN
        proc1_max = proc_x_max
      ELSE
        proc1_max = MPI_PROC_NULL
      END IF
    END IF

    ALLOCATE(temp(ng))

    CALL MPI_SENDRECV(field(1), ng, basetype, proc1_min, &
        tag, temp, ng, basetype, proc1_max, tag, comm, status, errcode)

    IF (proc1_max /= MPI_PROC_NULL) THEN
      n = 1
      DO i = n1_local+1, ng+n1_local
        field(i) = temp(n)
        n = n + 1
      END DO
    END IF

    CALL MPI_SENDRECV(field(n1_local+1-ng), ng, basetype, proc1_max, &
        tag, temp, ng, basetype, proc1_min, tag, comm, status, errcode)

    IF (proc1_min /= MPI_PROC_NULL) THEN
      n = 1
      DO i = 1-ng, 0
        field(i) = temp(n)
        n = n + 1
      END DO
    END IF

    DEALLOCATE(temp)

  END SUBROUTINE do_field_mpi_with_lengths_slice



  SUBROUTINE do_field_mpi_with_lengths_slice_cmplx(field, direction, ng, &
      n1_local)

    INTEGER, INTENT(IN) :: direction, ng
    COMPLEX(num), DIMENSION(1-ng:), INTENT(INOUT) :: field
    INTEGER, INTENT(IN) :: n1_local
    INTEGER :: proc1_min, proc1_max
    INTEGER :: basetype, i, n
    COMPLEX(num), ALLOCATABLE :: temp(:)

    basetype = MPI_DOUBLE_COMPLEX

    IF (direction == c_dir_x) THEN
      IF (.NOT. y_min_boundary .OR. bc_field(c_bd_y_min) == c_bc_periodic) THEN
        proc1_min = proc_y_min
      ELSE
        proc1_min = MPI_PROC_NULL
      END IF

      IF (.NOT. y_max_boundary .OR. bc_field(c_bd_y_max) == c_bc_periodic) THEN
        proc1_max = proc_y_max
      ELSE
        proc1_max = MPI_PROC_NULL
      END IF
    ELSE
      IF (.NOT. x_min_boundary .OR. bc_field(c_bd_x_min) == c_bc_periodic) THEN
        proc1_min = proc_x_min
      ELSE
        proc1_min = MPI_PROC_NULL
      END IF
      IF (.NOT. x_max_boundary .OR. bc_field(c_bd_x_max) == c_bc_periodic) THEN
        proc1_max = proc_x_max
      ELSE
        proc1_max = MPI_PROC_NULL
      END IF
    END IF

    ALLOCATE(temp(ng))

    CALL MPI_SENDRECV(field(1), ng, basetype, proc1_min, &
        tag, temp, ng, basetype, proc1_max, tag, comm, status, errcode)

    IF (proc1_max /= MPI_PROC_NULL) THEN
      n = 1
      DO i = n1_local+1, ng+n1_local
        field(i) = temp(n)
        n = n + 1
      END DO
    END IF

    CALL MPI_SENDRECV(field(n1_local+1-ng), ng, basetype, proc1_max, &
        tag, temp, ng, basetype, proc1_min, tag, comm, status, errcode)

    IF (proc1_min /= MPI_PROC_NULL) THEN
      n = 1
      DO i = 1-ng, 0
        field(i) = temp(n)
        n = n + 1
      END DO
    END IF

    DEALLOCATE(temp)

  END SUBROUTINE do_field_mpi_with_lengths_slice_cmplx



  SUBROUTINE do_field_mpi_with_lengths(field, ng, nx_local, ny_local)

    INTEGER, INTENT(IN) :: ng
    REAL(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: field
    INTEGER, INTENT(IN) :: nx_local, ny_local
    INTEGER, DIMENSION(c_ndims) :: sizes, subsizes, starts
    INTEGER :: subarray, basetype, sz, szmax, i, j, n
    REAL(num), ALLOCATABLE :: temp(:)

    basetype = mpireal

    sizes(1) = nx_local + 2 * ng
    sizes(2) = ny_local + 2 * ng
    starts = 1

    szmax = sizes(1) * ng
    sz = sizes(2) * ng
    IF (sz > szmax) szmax = sz

    ALLOCATE(temp(szmax))

    subsizes(1) = ng
    subsizes(2) = sizes(2)

    sz = subsizes(1) * subsizes(2)

    subarray = create_2d_array_subtype(basetype, subsizes, sizes, starts)

    CALL MPI_SENDRECV(field(1,1-ng), 1, subarray, proc_x_min, &
        tag, temp, sz, basetype, proc_x_max, tag, comm, status, errcode)

    IF (.NOT. x_max_boundary .OR. bc_field(c_bd_x_max) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = nx_local+1, subsizes(1)+nx_local
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_SENDRECV(field(nx_local+1-ng,1-ng), 1, subarray, proc_x_max, &
        tag, temp, sz, basetype, proc_x_min, tag, comm, status, errcode)

    IF (.NOT. x_min_boundary .OR. bc_field(c_bd_x_min) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_TYPE_FREE(subarray, errcode)

    subsizes(1) = sizes(1)
    subsizes(2) = ng

    sz = subsizes(1) * subsizes(2)

    subarray = create_2d_array_subtype(basetype, subsizes, sizes, starts)

    CALL MPI_SENDRECV(field(1-ng,1), 1, subarray, proc_y_min, &
        tag, temp, sz, basetype, proc_y_max, tag, comm, status, errcode)

    IF (.NOT. y_max_boundary .OR. bc_field(c_bd_y_max) == c_bc_periodic) THEN
      n = 1
      DO j = ny_local+1, subsizes(2)+ny_local
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_SENDRECV(field(1-ng,ny_local+1-ng), 1, subarray, proc_y_max, &
        tag, temp, sz, basetype, proc_y_min, tag, comm, status, errcode)

    IF (.NOT. y_min_boundary .OR. bc_field(c_bd_y_min) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_TYPE_FREE(subarray, errcode)

    DEALLOCATE(temp)

  END SUBROUTINE do_field_mpi_with_lengths



  SUBROUTINE do_field_mpi_with_lengths_r4(field, ng, nx_local, ny_local)

    INTEGER, INTENT(IN) :: ng
    REAL(r4), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: field
    INTEGER, INTENT(IN) :: nx_local, ny_local
    INTEGER, DIMENSION(c_ndims) :: sizes, subsizes, starts
    INTEGER :: subarray, basetype, sz, szmax, i, j, n
    REAL(r4), ALLOCATABLE :: temp(:)

    basetype = MPI_REAL4

    sizes(1) = nx_local + 2 * ng
    sizes(2) = ny_local + 2 * ng
    starts = 1

    szmax = sizes(1) * ng
    sz = sizes(2) * ng
    IF (sz > szmax) szmax = sz

    ALLOCATE(temp(szmax))

    subsizes(1) = ng
    subsizes(2) = sizes(2)

    sz = subsizes(1) * subsizes(2)

    subarray = create_2d_array_subtype(basetype, subsizes, sizes, starts)

    CALL MPI_SENDRECV(field(1,1-ng), 1, subarray, proc_x_min, &
        tag, temp, sz, basetype, proc_x_max, tag, comm, status, errcode)

    IF (.NOT. x_max_boundary .OR. bc_field(c_bd_x_max) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = nx_local+1, subsizes(1)+nx_local
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_SENDRECV(field(nx_local+1-ng,1-ng), 1, subarray, proc_x_max, &
        tag, temp, sz, basetype, proc_x_min, tag, comm, status, errcode)

    IF (.NOT. x_min_boundary .OR. bc_field(c_bd_x_min) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_TYPE_FREE(subarray, errcode)

    subsizes(1) = sizes(1)
    subsizes(2) = ng

    sz = subsizes(1) * subsizes(2)

    subarray = create_2d_array_subtype(basetype, subsizes, sizes, starts)

    CALL MPI_SENDRECV(field(1-ng,1), 1, subarray, proc_y_min, &
        tag, temp, sz, basetype, proc_y_max, tag, comm, status, errcode)

    IF (.NOT. y_max_boundary .OR. bc_field(c_bd_y_max) == c_bc_periodic) THEN
      n = 1
      DO j = ny_local+1, subsizes(2)+ny_local
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_SENDRECV(field(1-ng,ny_local+1-ng), 1, subarray, proc_y_max, &
        tag, temp, sz, basetype, proc_y_min, tag, comm, status, errcode)

    IF (.NOT. y_min_boundary .OR. bc_field(c_bd_y_min) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_TYPE_FREE(subarray, errcode)

    DEALLOCATE(temp)

  END SUBROUTINE do_field_mpi_with_lengths_r4



  SUBROUTINE do_field_mpi_with_lengths_cmplx(field, ng, nx_local, ny_local)

    INTEGER, INTENT(IN) :: ng
    COMPLEX(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: field
    INTEGER, INTENT(IN) :: nx_local, ny_local
    INTEGER, DIMENSION(c_ndims) :: sizes, subsizes, starts
    INTEGER :: subarray, basetype, sz, szmax, i, j, n
    COMPLEX(num), ALLOCATABLE :: temp(:)

    basetype = MPI_DOUBLE_COMPLEX

    sizes(1) = nx_local + 2 * ng
    sizes(2) = ny_local + 2 * ng
    starts = 1

    szmax = sizes(1) * ng
    sz = sizes(2) * ng
    IF (sz > szmax) szmax = sz

    ALLOCATE(temp(szmax))

    subsizes(1) = ng
    subsizes(2) = sizes(2)

    sz = subsizes(1) * subsizes(2)

    subarray = create_2d_array_subtype(basetype, subsizes, sizes, starts)

    CALL MPI_SENDRECV(field(1,1-ng), 1, subarray, proc_x_min, &
        tag, temp, sz, basetype, proc_x_max, tag, comm, status, errcode)

    IF (.NOT. x_max_boundary .OR. bc_field(c_bd_x_max) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = nx_local+1, subsizes(1)+nx_local
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_SENDRECV(field(nx_local+1-ng,1-ng), 1, subarray, proc_x_max, &
        tag, temp, sz, basetype, proc_x_min, tag, comm, status, errcode)

    IF (.NOT. x_min_boundary .OR. bc_field(c_bd_x_min) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_TYPE_FREE(subarray, errcode)

    subsizes(1) = sizes(1)
    subsizes(2) = ng

    sz = subsizes(1) * subsizes(2)

    subarray = create_2d_array_subtype(basetype, subsizes, sizes, starts)

    CALL MPI_SENDRECV(field(1-ng,1), 1, subarray, proc_y_min, &
        tag, temp, sz, basetype, proc_y_max, tag, comm, status, errcode)

    IF (.NOT. y_max_boundary .OR. bc_field(c_bd_y_max) == c_bc_periodic) THEN
      n = 1
      DO j = ny_local+1, subsizes(2)+ny_local
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_SENDRECV(field(1-ng,ny_local+1-ng), 1, subarray, proc_y_max, &
        tag, temp, sz, basetype, proc_y_min, tag, comm, status, errcode)

    IF (.NOT. y_min_boundary .OR. bc_field(c_bd_y_min) == c_bc_periodic) THEN
      n = 1
      DO j = 1-ng, subsizes(2)-ng
      DO i = 1-ng, subsizes(1)-ng
        field(i,j) = temp(n)
        n = n + 1
      END DO
      END DO
    END IF

    CALL MPI_TYPE_FREE(subarray, errcode)

    DEALLOCATE(temp)

  END SUBROUTINE do_field_mpi_with_lengths_cmplx



  SUBROUTINE field_zero_gradient(field, stagger_type, boundary)

    REAL(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: field
    INTEGER, INTENT(IN) :: stagger_type, boundary
    INTEGER :: i, nn

    IF (bc_field(boundary) == c_bc_periodic) RETURN

    IF (boundary == c_bd_x_min .AND. x_min_boundary) THEN
      IF (stagger(c_dir_x,stagger_type)) THEN
        DO i = 1, ng-1
          field(i-ng,:) = field(ng-i,:)
        END DO
      ELSE
        DO i = 1, ng
          field(i-ng,:) = field(ng+1-i,:)
        END DO
      END IF
    ELSE IF (boundary == c_bd_x_max .AND. x_max_boundary) THEN
      nn = nx
      IF (stagger(c_dir_x,stagger_type)) THEN
        DO i = 1, ng-1
          field(nn+i,:) = field(nn-i,:)
        END DO
      ELSE
        DO i = 1, ng
          field(nn+i,:) = field(nn+1-i,:)
        END DO
      END IF

    ELSE IF (boundary == c_bd_y_min .AND. y_min_boundary) THEN
      IF (stagger(c_dir_y,stagger_type)) THEN
        DO i = 1, ng-1
          field(:,i-ng) = field(:,ng-i)
        END DO
      ELSE
        DO i = 1, ng
          field(:,i-ng) = field(:,ng+1-i)
        END DO
      END IF
    ELSE IF (boundary == c_bd_y_max .AND. y_max_boundary) THEN
      nn = ny
      IF (stagger(c_dir_y,stagger_type)) THEN
        DO i = 1, ng-1
          field(:,nn+i) = field(:,nn-i)
        END DO
      ELSE
        DO i = 1, ng
          field(:,nn+i) = field(:,nn+1-i)
        END DO
      END IF
    END IF

  END SUBROUTINE field_zero_gradient



  SUBROUTINE field_mode_zero_gradient(field, stagger_type, boundary)

    COMPLEX(num), DIMENSION(1-ng:,1-ng:,0:), INTENT(INOUT) :: field
    INTEGER, INTENT(IN) :: stagger_type, boundary
    INTEGER :: i, nn

    IF (bc_field(boundary) == c_bc_periodic) RETURN

    IF (boundary == c_bd_x_min .AND. x_min_boundary) THEN
      IF (stagger(c_dir_x,stagger_type)) THEN
        DO i = 1, ng-1
          field(i-ng,:,:) = field(ng-i,:,:)
        END DO
      ELSE
        DO i = 1, ng
          field(i-ng,:,:) = field(ng+1-i,:,:)
        END DO
      END IF
    ELSE IF (boundary == c_bd_x_max .AND. x_max_boundary) THEN
      nn = nx
      IF (stagger(c_dir_x,stagger_type)) THEN
        DO i = 1, ng-1
          field(nn+i,:,:) = field(nn-i,:,:)
        END DO
      ELSE
        DO i = 1, ng
          field(nn+i,:,:) = field(nn+1-i,:,:)
        END DO
      END IF

    ELSE IF (boundary == c_bd_y_min .AND. y_min_boundary) THEN
      IF (stagger(c_dir_y,stagger_type)) THEN
        DO i = 1, ng-1
          field(:,i-ng,:) = field(:,ng-i,:)
        END DO
      ELSE
        DO i = 1, ng
          field(:,i-ng,:) = field(:,ng+1-i,:)
        END DO
      END IF
    ELSE IF (boundary == c_bd_y_max .AND. y_max_boundary) THEN
      nn = ny
      IF (stagger(c_dir_y,stagger_type)) THEN
        DO i = 1, ng-1
          field(:,nn+i,:) = field(:,nn-i,:)
        END DO
      ELSE
        DO i = 1, ng
          field(:,nn+i,:) = field(:,nn+1-i,:)
        END DO
      END IF
    END IF

  END SUBROUTINE field_mode_zero_gradient



  SUBROUTINE field_clamp_zero(field, ng, stagger_type, boundary)

    INTEGER, INTENT(IN) :: ng, stagger_type, boundary
    REAL(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: field
    INTEGER :: i, nn

    IF (bc_field(boundary) == c_bc_periodic) RETURN

    IF (boundary == c_bd_x_min .AND. x_min_boundary) THEN
      IF (stagger(c_dir_x,stagger_type)) THEN
        DO i = 1, ng-1
          field(i-ng,:) = -field(ng-i,:)
        END DO
        field(0,:) = 0.0_num
      ELSE
        DO i = 1, ng
          field(i-ng,:) = -field(ng+1-i,:)
        END DO
      END IF
    ELSE IF (boundary == c_bd_x_max .AND. x_max_boundary) THEN
      nn = nx
      IF (stagger(c_dir_x,stagger_type)) THEN
        field(nn,:) = 0.0_num
        DO i = 1, ng-1
          field(nn+i,:) = -field(nn-i,:)
        END DO
      ELSE
        DO i = 1, ng
          field(nn+i,:) = -field(nn+1-i,:)
        END DO
      END IF

    ELSE IF (boundary == c_bd_y_min .AND. y_min_boundary) THEN
      IF (stagger(c_dir_y,stagger_type)) THEN
        DO i = 1, ng-1
          field(:,i-ng) = -field(:,ng-i)
        END DO
        field(:,0) = 0.0_num
      ELSE
        DO i = 1, ng
          field(:,i-ng) = -field(:,ng+1-i)
        END DO
      END IF
    ELSE IF (boundary == c_bd_y_max .AND. y_max_boundary) THEN
      nn = ny
      IF (stagger(c_dir_y,stagger_type)) THEN
        field(:,nn) = 0.0_num
        DO i = 1, ng-1
          field(:,nn+i) = -field(:,nn-i)
        END DO
      ELSE
        DO i = 1, ng
          field(:,nn+i) = -field(:,nn+1-i)
        END DO
      END IF
    END IF

  END SUBROUTINE field_clamp_zero



  SUBROUTINE field_mode_clamp_zero(field, ng, stagger_type, boundary)

    INTEGER, INTENT(IN) :: ng, stagger_type, boundary
    COMPLEX(num), DIMENSION(1-ng:,1-ng:,0:), INTENT(INOUT) :: field
    INTEGER :: i, nn

    IF (bc_field(boundary) == c_bc_periodic) RETURN

    IF (boundary == c_bd_x_min .AND. x_min_boundary) THEN
      IF (stagger(c_dir_x,stagger_type)) THEN
        DO i = 1, ng-1
          field(i-ng,:,:) = -field(ng-i,:,:)
        END DO
        field(0,:,:) = 0.0_num
      ELSE
        DO i = 1, ng
          field(i-ng,:,:) = -field(ng+1-i,:,:)
        END DO
      END IF
    ELSE IF (boundary == c_bd_x_max .AND. x_max_boundary) THEN
      nn = nx
      IF (stagger(c_dir_x,stagger_type)) THEN
        field(nn,:,:) = 0.0_num
        DO i = 1, ng-1
          field(nn+i,:,:) = -field(nn-i,:,:)
        END DO
      ELSE
        DO i = 1, ng
          field(nn+i,:,:) = -field(nn+1-i,:,:)
        END DO
      END IF

    ELSE IF (boundary == c_bd_y_min .AND. y_min_boundary) THEN
      IF (stagger(c_dir_y,stagger_type)) THEN
        DO i = 1, ng-1
          field(:,i-ng,:) = -field(:,ng-i,:)
        END DO
        field(:,0,:) = 0.0_num
      ELSE
        DO i = 1, ng
          field(:,i-ng,:) = -field(:,ng+1-i,:)
        END DO
      END IF
    ELSE IF (boundary == c_bd_y_max .AND. y_max_boundary) THEN
      nn = ny
      IF (stagger(c_dir_y,stagger_type)) THEN
        field(:,nn,:) = 0.0_num
        DO i = 1, ng-1
          field(:,nn+i,:) = -field(:,nn-i,:)
        END DO
      ELSE
        DO i = 1, ng
          field(:,nn+i,:) = -field(:,nn+1-i,:)
        END DO
      END IF
    END IF

  END SUBROUTINE field_mode_clamp_zero



  SUBROUTINE particle_reflection_bcs(array, ng, flip_direction, species)

    INTEGER, INTENT(IN) :: ng
    REAL(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: array
    INTEGER, INTENT(IN), OPTIONAL :: flip_direction
    INTEGER, INTENT(IN), OPTIONAL :: species
    INTEGER, DIMENSION(c_ndims) :: sizes
    INTEGER :: nn, n, i, flip_dir, bc
    INTEGER, DIMENSION(2*c_ndims) :: bc_species

    flip_dir = 0
    IF (PRESENT(flip_direction)) flip_dir = flip_direction

    bc_species = bc_allspecies
    IF (PRESENT(species)) THEN
      DO i = 1, 2*c_ndims
        IF (bc_species(i) == c_bc_mixed) THEN
          bc_species(i) = species_list(species)%bc_particle(i)
        END IF
      END DO
    END IF

    sizes = SHAPE(array)
    n = 0
    nn = sizes(n/2+1) - 2 * ng

    n = n + 1
    bc = bc_species(n)
    IF (x_min_boundary .AND. bc == c_bc_reflect) THEN
      IF (flip_dir == (n-1)/2 + 1) THEN
        ! Currents get reversed in the direction of the boundary
        DO i = 1, ng-1
          array(i,:) = array(i,:) - array(-i,:)
          array(-i,:) = 0.0_num
        END DO
      ELSE
        DO i = 1, ng-1
          array(i,:) = array(i,:) + array(1-i,:)
          array(1-i,:) = 0.0_num
        END DO
      END IF
    END IF

    n = n + 1
    bc = bc_species(n)
    IF (x_max_boundary .AND. bc == c_bc_reflect) THEN
      IF (flip_dir == (n-1)/2 + 1) THEN
        ! Currents get reversed in the direction of the boundary
        DO i = 1, ng
          array(nn-i,:) = array(nn-i,:) - array(nn+i,:)
          array(nn+i,:) = 0.0_num
        END DO
      ELSE
        DO i = 1, ng
          array(nn+1-i,:) = array(nn+1-i,:) + array(nn+i,:)
          array(nn+i,:) = 0.0_num
        END DO
      END IF
    END IF

    nn = sizes(n/2+1) - 2 * ng

    ! Treat r=0 axis separately

    n = n + 2
    bc = bc_species(n)
    IF (y_max_boundary .AND. bc == c_bc_reflect) THEN
      IF (flip_dir == (n-1)/2 + 1) THEN
        ! Currents get reversed in the direction of the boundary
        DO i = 1, ng
          array(:,nn-i) = array(:,nn-i) - array(:,nn+i)
          array(:,nn+i) = 0.0_num
        END DO
      ELSE
        DO i = 1, ng
          array(:,nn+1-i) = array(:,nn+1-i) + array(:,nn+i)
          array(:,nn+i) = 0.0_num
        END DO
      END IF
    END IF

  END SUBROUTINE particle_reflection_bcs



  SUBROUTINE particle_reflection_bcs_complex(array, ng, flip_direction, species)

    INTEGER, INTENT(IN) :: ng
    COMPLEX(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: array
    INTEGER, INTENT(IN), OPTIONAL :: flip_direction
    INTEGER, INTENT(IN), OPTIONAL :: species
    INTEGER, DIMENSION(c_ndims) :: sizes
    INTEGER :: nn, n, i, flip_dir, bc
    INTEGER, DIMENSION(2*c_ndims) :: bc_species
    REAL(num) :: r_max

    flip_dir = 0
    IF (PRESENT(flip_direction)) flip_dir = flip_direction

    bc_species = bc_allspecies
    IF (PRESENT(species)) THEN
      DO i = 1, 2*c_ndims
        IF (bc_species(i) == c_bc_mixed) THEN
          bc_species(i) = species_list(species)%bc_particle(i)
        END IF
      END DO
    END IF

    sizes = SHAPE(array)
    n = 0
    nn = sizes(n/2+1) - 2 * ng

    n = n + 1
    bc = bc_species(n)
    IF (x_min_boundary .AND. bc == c_bc_reflect) THEN
      IF (flip_dir == (n-1)/2 + 1) THEN
        ! Currents get reversed in the direction of the boundary
        DO i = 1, ng-1
          array(i,:) = array(i,:) - array(1-i,:)
          array(1-i,:) = 0.0_num
        END DO
      ELSE
        DO i = 1, ng-1
          array(i,:) = array(i,:) + array(-i,:)
          array(-i,:) = 0.0_num
        END DO
      END IF
    END IF

    n = n + 1
    bc = bc_species(n)
    IF (x_max_boundary .AND. bc == c_bc_reflect) THEN
      IF (flip_dir == (n-1)/2 + 1) THEN
        ! Currents get reversed in the direction of the boundary
        DO i = 1, ng
          array(nn+1-i,:) = array(nn+1-i,:) - array(nn+i,:)
          array(nn+i,:) = 0.0_num
        END DO
      ELSE
        DO i = 1, ng
          array(nn-i,:) = array(nn-i,:) + array(nn+i,:)
          array(nn+i,:) = 0.0_num
        END DO
      END IF
    END IF

    nn = sizes(n/2+1) - 2 * ng

    ! Treat r=0 axis separately

    ! For radial reflection, we must consider different areas of cells
    n = n + 2
    bc = bc_species(n)
    IF (y_max_boundary .AND. bc == c_bc_reflect) THEN
      ! Currents get reversed in the direction of the boundary
      IF (flip_dir == c_dir_y) THEN
        ! Jrm reflection
        r_max = y_grid_min_local + (REAL(ny,num)-0.5_num)*dy
        DO i = 1, ng
          array(:,nn+1-i) = array(:,nn+1-i) - array(:,nn+i) &
              * (r_max + (REAL(i,num)-0.5_num)*dy) &
              / (r_max - (REAL(i,num)-0.5_num)*dy)
          array(:,nn+i) = 0.0_num
        END DO
      ELSE IF (flip_dir == c_dir_x) THEN
        ! Jxm reflection
        r_max = y_grid_min_local + (REAL(ny,num)-0.5_num)*dy
        DO i = 1, ng
          array(:,nn-i) = array(:,nn-i) + array(:,nn+i) &
            * (r_max + REAL(i,num)*dy) &
            / (r_max - REAL(i,num)*dy)
          array(:,nn+i) = 0.0_num
        END DO
      ELSE 
        ! Jtm reflection
        DO i = 1, ng 
          array(:,nn-i) = array(:,nn-i) + array(:,nn+i)
          array(:,nn+i) = 0.0_num
        END DO
      END IF
    END IF

  END SUBROUTINE particle_reflection_bcs_complex



  SUBROUTINE particle_periodic_bcs(array, ng, species)

    INTEGER, INTENT(IN) :: ng
    REAL(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: array
    INTEGER, INTENT(IN), OPTIONAL :: species
    REAL(num), DIMENSION(:,:), ALLOCATABLE :: temp
    INTEGER, DIMENSION(c_ndims) :: sizes, subsizes, starts
    INTEGER :: n, nn, sz, subarray, i
    INTEGER, DIMENSION(-1:1) :: neighbour_local
    INTEGER, DIMENSION(2*c_ndims) :: bc_species

    ! Transmit and sum all boundaries.
    ! Set neighbour to MPI_PROC_NULL if we don't need to transfer anything

    bc_species = bc_allspecies
    IF (PRESENT(species)) THEN
      DO i = 1, 2*c_ndims
        IF (bc_species(i) == c_bc_mixed) THEN
          bc_species(i) = species_list(species)%bc_particle(i)
        END IF
      END DO
    END IF

    sizes = SHAPE(array)
    starts = 1
    n = 0

    subsizes = sizes
    subsizes(n/2+1) = ng
    nn = sizes(n/2+1) - 2 * ng

    subarray = create_2d_array_subtype(mpireal, subsizes, sizes, starts)

    sz = subsizes(1) * subsizes(2)
    ALLOCATE(temp(subsizes(1), subsizes(2)))

    ! Don't bother communicating non-periodic boundaries
    neighbour_local = neighbour(:,0)
    n = n + 1
    IF (x_min_boundary) THEN
      IF (bc_species(n) /= c_bc_periodic) THEN
        neighbour_local(-1) = MPI_PROC_NULL
      END IF
    END IF
    n = n + 1
    IF (x_max_boundary) THEN
      IF (bc_species(n) /= c_bc_periodic) THEN
        neighbour_local( 1) = MPI_PROC_NULL
      END IF
    END IF
    n = n - 2

    temp = 0.0_num
    CALL MPI_SENDRECV(array(nn+1,1-ng), 1, subarray, &
        neighbour_local( 1), tag, temp, sz, mpireal, &
        neighbour_local(-1), tag, comm, status, errcode)

    n = n + 1
    array(1:ng,:) = array(1:ng,:) + temp

    temp = 0.0_num
    CALL MPI_SENDRECV(array(1-ng,1-ng), 1, subarray, &
        neighbour_local(-1), tag, temp, sz, mpireal, &
        neighbour_local( 1), tag, comm, status, errcode)

    n = n + 1
    array(nn+1-ng:nn,:) = array(nn+1-ng:nn,:) + temp

    DEALLOCATE(temp)
    CALL MPI_TYPE_FREE(subarray, errcode)

    subsizes = sizes
    subsizes(n/2+1) = ng
    nn = sizes(n/2+1) - 2 * ng

    subarray = create_2d_array_subtype(mpireal, subsizes, sizes, starts)

    sz = subsizes(1) * subsizes(2)
    ALLOCATE(temp(subsizes(1), subsizes(2)))

    ! r_max boundary cannot be periodic
    neighbour_local = neighbour(0,:)
    IF (y_min_boundary) THEN
      neighbour_local(-1) = MPI_PROC_NULL
    END IF
    IF (y_max_boundary) THEN
      neighbour_local( 1) = MPI_PROC_NULL
    END IF

    temp = 0.0_num
    CALL MPI_SENDRECV(array(1-ng,nn+1), 1, subarray, &
        neighbour_local( 1), tag, temp, sz, mpireal, &
        neighbour_local(-1), tag, comm, status, errcode)

    n = n + 1
    array(:,1:ng) = array(:,1:ng) + temp

    temp = 0.0_num
    CALL MPI_SENDRECV(array(1-ng,1-ng), 1, subarray, &
        neighbour_local(-1), tag, temp, sz, mpireal, &
        neighbour_local( 1), tag, comm, status, errcode)

    n = n + 1
    array(:,nn+1-ng:nn) = array(:,nn+1-ng:nn) + temp

    DEALLOCATE(temp)
    CALL MPI_TYPE_FREE(subarray, errcode)

    IF (PRESENT(species)) CALL particle_clear_bcs(array, ng)

  END SUBROUTINE particle_periodic_bcs



  SUBROUTINE particle_periodic_bcs_complex(array, ng, species)

    INTEGER, INTENT(IN) :: ng
    COMPLEX(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: array
    INTEGER, INTENT(IN), OPTIONAL :: species
    COMPLEX(num), DIMENSION(:,:), ALLOCATABLE :: temp
    INTEGER, DIMENSION(c_ndims) :: sizes, subsizes, starts
    INTEGER :: n, nn, sz, subarray, i
    INTEGER, DIMENSION(-1:1) :: neighbour_local
    INTEGER, DIMENSION(2*c_ndims) :: bc_species

    ! Transmit and sum all boundaries.
    ! Set neighbour to MPI_PROC_NULL if we don't need to transfer anything

    bc_species = bc_allspecies
    IF (PRESENT(species)) THEN
      DO i = 1, 2*c_ndims
        IF (bc_species(i) == c_bc_mixed) THEN
          bc_species(i) = species_list(species)%bc_particle(i)
        END IF
      END DO
    END IF

    sizes = SHAPE(array)
    starts = 1
    n = 0

    subsizes = sizes
    subsizes(n/2+1) = ng
    nn = sizes(n/2+1) - 2 * ng

    subarray = create_2d_array_subtype(MPI_DOUBLE_COMPLEX, subsizes, sizes, &
        starts)

    sz = subsizes(1) * subsizes(2)
    ALLOCATE(temp(subsizes(1), subsizes(2)))

    ! Don't bother communicating non-periodic boundaries
    neighbour_local = neighbour(:,0)
    n = n + 1
    IF (x_min_boundary) THEN
      IF (bc_species(n) /= c_bc_periodic) THEN
        neighbour_local(-1) = MPI_PROC_NULL
      END IF
    END IF
    n = n + 1
    IF (x_max_boundary) THEN
      IF (bc_species(n) /= c_bc_periodic) THEN
        neighbour_local( 1) = MPI_PROC_NULL
      END IF
    END IF
    n = n - 2

    temp = 0.0_num
    CALL MPI_SENDRECV(array(nn+1,1-ng), 1, subarray, &
        neighbour_local( 1), tag, temp, sz, MPI_DOUBLE_COMPLEX, &
        neighbour_local(-1), tag, comm, status, errcode)

    n = n + 1
    array(1:ng,:) = array(1:ng,:) + temp

    temp = 0.0_num
    CALL MPI_SENDRECV(array(1-ng,1-ng), 1, subarray, &
        neighbour_local(-1), tag, temp, sz, MPI_DOUBLE_COMPLEX, &
        neighbour_local( 1), tag, comm, status, errcode)

    n = n + 1
    array(nn+1-ng:nn,:) = array(nn+1-ng:nn,:) + temp

    DEALLOCATE(temp)
    CALL MPI_TYPE_FREE(subarray, errcode)

    subsizes = sizes
    subsizes(n/2+1) = ng
    nn = sizes(n/2+1) - 2 * ng

    subarray = create_2d_array_subtype(MPI_DOUBLE_COMPLEX, subsizes, sizes, &
        starts)

    sz = subsizes(1) * subsizes(2)
    ALLOCATE(temp(subsizes(1), subsizes(2)))

    ! r_max boundary cannot be periodic
    neighbour_local = neighbour(0,:)
    IF (y_min_boundary) THEN
      neighbour_local(-1) = MPI_PROC_NULL
    END IF
    IF (y_max_boundary) THEN
      neighbour_local( 1) = MPI_PROC_NULL
    END IF

    temp = 0.0_num
    CALL MPI_SENDRECV(array(1-ng,nn+1), 1, subarray, &
        neighbour_local( 1), tag, temp, sz, MPI_DOUBLE_COMPLEX, &
        neighbour_local(-1), tag, comm, status, errcode)

    n = n + 1
    array(:,1:ng) = array(:,1:ng) + temp

    temp = 0.0_num
    CALL MPI_SENDRECV(array(1-ng,1-ng), 1, subarray, &
        neighbour_local(-1), tag, temp, sz, MPI_DOUBLE_COMPLEX, &
        neighbour_local( 1), tag, comm, status, errcode)

    n = n + 1
    array(:,nn+1-ng:nn) = array(:,nn+1-ng:nn) + temp

    DEALLOCATE(temp)
    CALL MPI_TYPE_FREE(subarray, errcode)

    IF (PRESENT(species)) CALL particle_clear_bcs_complex(array, ng)

  END SUBROUTINE particle_periodic_bcs_complex



  SUBROUTINE particle_clear_bcs(array, ng)

    INTEGER, INTENT(IN) :: ng
    REAL(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: array
    INTEGER, DIMENSION(c_ndims) :: sizes
    INTEGER :: n, nn

    sizes = SHAPE(array)
    n = 0

    nn = sizes(n/2+1) - 2 * ng

    n = n + 1
    array(:0,:) = 0.0_num
    n = n + 1
    array(nn+1:,:) = 0.0_num

    nn = sizes(n/2+1) - 2 * ng

    n = n + 1
    array(:,:0) = 0.0_num
    n = n + 1
    array(:,nn+1:) = 0.0_num

  END SUBROUTINE particle_clear_bcs



  SUBROUTINE particle_clear_bcs_complex(array, ng)

    INTEGER, INTENT(IN) :: ng
    COMPLEX(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: array
    INTEGER, DIMENSION(c_ndims) :: sizes
    INTEGER :: n, nn

    sizes = SHAPE(array)
    n = 0

    nn = sizes(n/2+1) - 2 * ng

    n = n + 1
    array(:0,:) = 0.0_num
    n = n + 1
    array(nn+1:,:) = 0.0_num

    nn = sizes(n/2+1) - 2 * ng

    n = n + 1
    array(:,:0) = 0.0_num
    n = n + 1
    array(:,nn+1:) = 0.0_num

  END SUBROUTINE particle_clear_bcs_complex



  SUBROUTINE processor_summation_bcs(array, ng, flip_direction, species)

    INTEGER, INTENT(IN) :: ng
    REAL(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: array
    INTEGER, INTENT(IN), OPTIONAL :: flip_direction
    INTEGER, INTENT(IN), OPTIONAL :: species

    IF (PRESENT(species) .AND. .NOT. ANY(bc_allspecies == c_bc_mixed)) THEN
      RETURN
    END IF

    IF (.NOT. PRESENT(species) .AND. ANY(bc_allspecies == c_bc_mixed)) THEN
      RETURN
    END IF

    ! First apply reflecting boundary conditions
    CALL particle_reflection_bcs(array, ng, flip_direction, species)

    ! Next apply periodic and subdomain boundary conditions
    CALL particle_periodic_bcs(array, ng, species)

  END SUBROUTINE processor_summation_bcs



  SUBROUTINE processor_summation_bcs_complex(array, ng, flip_direction, species)

    INTEGER, INTENT(IN) :: ng
    COMPLEX(num), DIMENSION(1-ng:,1-ng:), INTENT(INOUT) :: array
    INTEGER, INTENT(IN), OPTIONAL :: flip_direction
    INTEGER, INTENT(IN), OPTIONAL :: species

    IF (PRESENT(species) .AND. .NOT. ANY(bc_allspecies == c_bc_mixed)) THEN
      RETURN
    END IF

    IF (.NOT. PRESENT(species) .AND. ANY(bc_allspecies == c_bc_mixed)) THEN
      RETURN
    END IF

    ! First apply reflecting boundary conditions
    CALL particle_reflection_bcs_complex(array, ng, flip_direction, species)

    ! Next apply periodic and subdomain boundary conditions
    CALL particle_periodic_bcs_complex(array, ng, species)

  END SUBROUTINE processor_summation_bcs_complex



  SUBROUTINE efield_bcs

    INTEGER :: i
    COMPLEX(num), ALLOCATABLE :: temp_array(:,:,:)

    ! These are the MPI boundaries. A shift is required on r-staggered variables
    ! as the ir = 0 index is not a ghost cell in cylindrical
    ALLOCATE(temp_array(1-ng:nx+ng, 1-ng:ny+ng, 0:n_mode-1))
    temp_array(:,2-ng:ny+ng,:) = exm(:,1-ng:ny+ng-1,:)
    CALL field_mode_bc(temp_array, ng)
    exm(:,1-ng:ny+ng-1,:) = temp_array(:,2-ng:ny+ng,:)
    
    CALL field_mode_bc(erm, ng)
    
    temp_array(:,2-ng:ny+ng,:) = etm(:,1-ng:ny+ng-1,:)
    CALL field_mode_bc(temp_array, ng)
    etm(:,1-ng:ny+ng-1,:) = temp_array(:,2-ng:ny+ng,:)
    DEALLOCATE(temp_array)

    ! Perfectly conducting boundaries
    DO i = c_bd_x_min, c_bd_x_max, c_bd_x_max - c_bd_x_min
      IF (bc_field(i) == c_bc_conduct) THEN
        CALL field_mode_clamp_zero(exm, ng, c_stagger_exm, i)
        CALL field_mode_zero_gradient(erm, c_stagger_erm, i)
        CALL field_mode_zero_gradient(etm, c_stagger_etm, i)
      END IF
    END DO

    i = c_bd_y_max
    IF (bc_field(i) == c_bc_conduct) THEN
      CALL field_mode_zero_gradient(exm, c_stagger_exm, i)
      CALL field_mode_clamp_zero(erm, ng, c_stagger_erm, i)
      CALL field_mode_zero_gradient(etm, c_stagger_etm, i)
    END IF

    DO i = 1, 2*c_ndims
      ! r=0 boundary conditions handled in fields.f90
      IF (i == c_bd_y_min) CYCLE

      ! These apply zero field boundary conditions on the edges
      IF (bc_field(i) == c_bc_clamp &
          .OR. bc_field(i) == c_bc_simple_laser &
          .OR. bc_field(i) == c_bc_simple_outflow) THEN
        CALL field_mode_clamp_zero(exm, ng, c_stagger_exm, i)
        CALL field_mode_clamp_zero(erm, ng, c_stagger_erm, i)
        CALL field_mode_clamp_zero(etm, ng, c_stagger_etm, i)
      END IF

      ! These apply zero gradient boundary conditions on the edges
      IF (bc_field(i) == c_bc_zero_gradient &
          .OR. bc_field(i) == c_bc_cpml_laser &
          .OR. bc_field(i) == c_bc_cpml_outflow) THEN
        CALL field_mode_zero_gradient(exm, c_stagger_exm, i)
        CALL field_mode_zero_gradient(erm, c_stagger_erm, i)
        CALL field_mode_zero_gradient(etm, c_stagger_etm, i)
      END IF
    END DO

  END SUBROUTINE efield_bcs



  SUBROUTINE bfield_bcs(mpi_only)

    LOGICAL, INTENT(IN) :: mpi_only
    INTEGER :: i
    COMPLEX(num), ALLOCATABLE :: temp_array(:,:,:)

    ! These are the MPI boundaries. A shift is required on r-staggered variables
    ! as the ir = 0 index is not a ghost cell in cylindrical
    ALLOCATE(temp_array(1-ng:nx+ng, 1-ng:ny+ng, 0:n_mode-1))
    CALL field_mode_bc(bxm, ng)

    temp_array(:,2-ng:ny+ng,:) = brm(:,1-ng:ny+ng-1,:)
    CALL field_mode_bc(temp_array, ng)
    brm(:,1-ng:ny+ng-1,:) = temp_array(:,2-ng:ny+ng,:)

    CALL field_mode_bc(btm, ng)
    DEALLOCATE(temp_array)

    IF (mpi_only) RETURN

    ! Perfectly conducting boundaries
    DO i = c_bd_x_min, c_bd_x_max, c_bd_x_max - c_bd_x_min
      IF (bc_field(i) == c_bc_conduct) THEN
        CALL field_mode_zero_gradient(bxm, c_stagger_bxm, i)
        CALL field_mode_clamp_zero(brm, ng, c_stagger_brm, i)
        CALL field_mode_clamp_zero(btm, ng, c_stagger_btm, i)
      END IF
    END DO

    i = c_bd_y_max
    IF (bc_field(i) == c_bc_conduct) THEN
      CALL field_mode_clamp_zero(bxm, ng, c_stagger_bxm, i)
      CALL field_mode_zero_gradient(brm, c_stagger_brm, i)
      CALL field_mode_clamp_zero(btm, ng, c_stagger_btm, i)
    END IF

    DO i = 1, 2*c_ndims
      ! r=0 boundary conditions handled in fields.f90
      IF (i == c_bd_y_min) CYCLE

      ! These apply zero field boundary conditions on the edges
      IF (bc_field(i) == c_bc_clamp &
          .OR. bc_field(i) == c_bc_simple_laser &
          .OR. bc_field(i) == c_bc_simple_outflow) THEN
        CALL field_mode_clamp_zero(bxm, ng, c_stagger_bxm, i)
        CALL field_mode_clamp_zero(brm, ng, c_stagger_brm, i)
        CALL field_mode_clamp_zero(btm, ng, c_stagger_btm, i)
      END IF

      ! These apply zero gradient boundary conditions on the edges
      IF (bc_field(i) == c_bc_zero_gradient &
          .OR. bc_field(i) == c_bc_cpml_laser &
          .OR. bc_field(i) == c_bc_cpml_outflow) THEN
        CALL field_mode_zero_gradient(bxm, c_stagger_bxm, i)
        CALL field_mode_zero_gradient(brm, c_stagger_brm, i)
        CALL field_mode_zero_gradient(btm, c_stagger_btm, i)
      END IF
    END DO

  END SUBROUTINE bfield_bcs



  SUBROUTINE old_field_bcs

    ! On restart, we need to correctly handle the old-B and old-J arrays, as we
    ! need to fill the ir = ny index on ranks which aren't on rmax

    INTEGER :: i
    COMPLEX(num), ALLOCATABLE :: temp_array(:,:,:)

    ! These are the MPI boundaries. A shift is required on r-staggered variables
    ! as the ir = 0 index is not a ghost cell in cylindrical
    ALLOCATE(temp_array(1-ng:nx+ng, 1-ng:ny+ng, 0:n_mode-1))
    CALL field_mode_bc(bxm_old, ng)

    temp_array(:,2-ng:ny+ng,:) = brm_old(:,1-ng:ny+ng-1,:)
    CALL field_mode_bc(temp_array, ng)
    brm_old(:,1-ng:ny+ng-1,:) = temp_array(:,2-ng:ny+ng,:)

    CALL field_mode_bc(btm_old, ng)
    DEALLOCATE(temp_array)


  END SUBROUTINE old_field_bcs



  SUBROUTINE bfield_final_bcs

    INTEGER :: i

    CALL update_laser_omegas
    CALL bfield_bcs(.FALSE.)

    IF (x_min_boundary) THEN
      i = c_bd_x_min
      IF (add_laser(i) .OR. bc_field(i) == c_bc_simple_outflow) &
          CALL outflow_bcs_x_min
    END IF

    IF (x_max_boundary) THEN
      i = c_bd_x_max
      IF (add_laser(i) .OR. bc_field(i) == c_bc_simple_outflow) &
          CALL outflow_bcs_x_max
    END IF

    IF (y_max_boundary) THEN
      i = c_bd_y_max
      IF (bc_field(i) == c_bc_simple_outflow) THEN
        CALL outflow_bcs_r_max
      ELSE IF (bc_field(i) == c_bc_zero_b) THEN 
        bxm(:,ny,:) = 0.0_num
        brm(:,ny,:) = 0.0_num
        btm(:,ny,:) = 0.0_num
      END IF
    END IF

    CALL bfield_bcs(.TRUE.)

  END SUBROUTINE bfield_final_bcs



  SUBROUTINE particle_bcs

    TYPE(particle), POINTER :: cur, next
    TYPE(particle_list), DIMENSION(-1:1,-1:1) :: send, recv
    INTEGER :: xbd, ybd
    INTEGER(i8) :: ixp, iyp
    INTEGER, DIMENSION(2*c_ndims) :: bc_species
    LOGICAL :: out_of_bounds
    INTEGER :: sgn, bc, ispecies, i, ix, iy
    INTEGER :: cell_x, cell_y
    REAL(num), DIMENSION(-1:1) :: gx, gy
    REAL(num) :: cell_x_r, cell_frac_x
    REAL(num) :: cell_y_r, cell_frac_y
    REAL(num) :: cf2, temp(3)
    REAL(num) :: part_pos, boundary_shift
    REAL(num) :: x_min_outer, x_max_outer, y_min_outer, y_max_outer
    REAL(num) :: x_shift, y_shift
    REAL(num) :: radial_reduction
    REAL(num) :: inv_final_r, cos_theta, sin_theta, part_pr, part_pt

    boundary_shift = dx * REAL((1 + png + cpml_thickness) / 2, num)
    x_min_outer = x_min - boundary_shift
    x_max_outer = x_max + boundary_shift
    x_shift = length_x + 2.0_num * dx * REAL(cpml_thickness, num)

    boundary_shift = dy * REAL((1 + png + cpml_thickness) / 2, num)
    y_min_outer = y_min - boundary_shift
    y_max_outer = y_max + boundary_shift
    y_shift = length_y + 2.0_num * dy * REAL(cpml_thickness, num)

    DO ispecies = 1, n_species
      cur => species_list(ispecies)%attached_list%head

      bc_species = species_list(ispecies)%bc_particle

      DO iy = -1, 1
        DO ix = -1, 1
          IF (ABS(ix) + ABS(iy) == 0) CYCLE
          CALL create_empty_partlist(send(ix, iy))
          CALL create_empty_partlist(recv(ix, iy))
        END DO
      END DO

      DO WHILE (ASSOCIATED(cur))
        next => cur%next

        xbd = 0
        ybd = 0
        out_of_bounds = .FALSE.

        part_pos = cur%part_pos(1)
        sgn = -1
        IF (bc_field(c_bd_x_min) == c_bc_cpml_laser &
            .OR. bc_field(c_bd_x_min) == c_bc_cpml_outflow) THEN
          IF (x_min_boundary) THEN
            ! Particle has left the system
            IF (part_pos < x_min_outer) THEN
              xbd = 0
              out_of_bounds = .TRUE.
            END IF
          ELSE
            ! Particle has left this processor
            IF (part_pos < x_min_local) xbd = sgn
          END IF
        ELSE
          ! Particle has left this processor
          IF (part_pos < x_min_local) THEN
            xbd = sgn
            ! Particle has left the system
            IF (x_min_boundary) THEN
              xbd = 0
              bc = bc_species(c_bd_x_min)
              IF (bc == c_bc_reflect) THEN
                cur%part_pos(1) = 2.0_num * x_min - part_pos
                cur%part_p(1) = -cur%part_p(1)
              ELSE IF (bc == c_bc_periodic) THEN
                xbd = sgn
                cur%part_pos(1) = part_pos - sgn * x_shift
              END IF
            END IF
            IF (part_pos < x_min_outer .AND. bc /= c_bc_periodic) THEN
              IF (bc == c_bc_thermal) THEN
                ! Always use the triangle particle weighting for simplicity
                cell_y_r = (cur%part_pos(2) - y_grid_min_local) / dy
                cell_y = FLOOR(cell_y_r + 0.5_num)
                cell_frac_y = REAL(cell_y, num) - cell_y_r
                cell_y = cell_y + 1

                cf2 = cell_frac_y**2
                gy(-1) = 0.5_num * (0.25_num + cf2 + cell_frac_y)
                gy( 0) = 0.75_num - cf2
                gy( 1) = 0.5_num * (0.25_num + cf2 - cell_frac_y)

                DO i = 1, 3
                  temp(i) = 0.0_num
                  DO iy = -1, 1
                    temp(i) = temp(i) + gy(iy) &
                        * species_list(ispecies)%ext_temp_x_min(cell_y+iy, i)
                  END DO
                END DO

                CALL id_registry%delete_all(cur)

                ! x-direction
                i = 1
                cur%part_p(i) = flux_momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num, &
                    -REAL(sgn, num))

                ! y-direction
                i = 2
                cur%part_p(i) = momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num)

                ! z-direction
                i = 3
                cur%part_p(i) = momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num)

                cur%part_pos(1) = 2.0_num * x_min_outer - part_pos

              ELSE
                ! Default to open boundary conditions - remove particle
                out_of_bounds = .TRUE.
              END IF
            END IF
          END IF
        END IF

        sgn = 1
        IF (bc_field(c_bd_x_max) == c_bc_cpml_laser &
            .OR. bc_field(c_bd_x_max) == c_bc_cpml_outflow) THEN
          IF (x_max_boundary) THEN
            ! Particle has left the system
            IF (part_pos >= x_max_outer) THEN
              xbd = 0
              out_of_bounds = .TRUE.
            END IF
          ELSE
            ! Particle has left this processor
            IF (part_pos >= x_max_local) xbd = sgn
          END IF
        ELSE
          ! Particle has left this processor
          IF (part_pos >= x_max_local) THEN
            xbd = sgn
            ! Particle has left the system
            IF (x_max_boundary) THEN
              xbd = 0
              bc = bc_species(c_bd_x_max)
              IF (bc == c_bc_reflect) THEN
                cur%part_pos(1) = 2.0_num * x_max - part_pos
                cur%part_p(1) = -cur%part_p(1)
              ELSE IF (bc == c_bc_periodic) THEN
                xbd = sgn
                cur%part_pos(1) = part_pos - sgn * x_shift
              END IF
            END IF
            IF (part_pos >= x_max_outer .AND. bc /= c_bc_periodic) THEN
              IF (bc == c_bc_thermal) THEN
                ! Always use the triangle particle weighting for simplicity
                cell_y_r = (cur%part_pos(2) - y_grid_min_local) / dy
                cell_y = FLOOR(cell_y_r + 0.5_num)
                cell_frac_y = REAL(cell_y, num) - cell_y_r
                cell_y = cell_y + 1

                cf2 = cell_frac_y**2
                gy(-1) = 0.5_num * (0.25_num + cf2 + cell_frac_y)
                gy( 0) = 0.75_num - cf2
                gy( 1) = 0.5_num * (0.25_num + cf2 - cell_frac_y)

                DO i = 1, 3
                  temp(i) = 0.0_num
                  DO iy = -1, 1
                    temp(i) = temp(i) + gy(iy) &
                        * species_list(ispecies)%ext_temp_x_max(cell_y+iy, i)
                  END DO
                END DO

                CALL id_registry%delete_all(cur)

                ! x-direction
                i = 1
                cur%part_p(i) = flux_momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num, &
                    -REAL(sgn, num))

                ! y-direction
                i = 2
                cur%part_p(i) = momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num)

                ! z-direction
                i = 3
                cur%part_p(i) = momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num)

                cur%part_pos(1) = 2.0_num * x_max_outer - part_pos

              ELSE
                ! Default to open boundary conditions - remove particle
                out_of_bounds = .TRUE.
              END IF
            END IF
          END IF
        END IF

        part_pos = SQRT(cur%part_pos(2)**2 + cur%part_pos(3)**2)
        sgn = -1
        ! Particle has left this processor, but cannot leave simulation on y_min
        IF (part_pos < y_min_local .AND. .NOT. y_min_boundary) THEN
          ybd = sgn
        END IF

        sgn = 1
        IF (bc_field(c_bd_y_max) == c_bc_cpml_laser &
            .OR. bc_field(c_bd_y_max) == c_bc_cpml_outflow) THEN
          IF (y_max_boundary) THEN
            ! Particle has left the system
            IF (part_pos >= y_max_outer) THEN
              ybd = 0
              out_of_bounds = .TRUE.
            END IF
          ELSE
            ! Particle has left this processor
            IF (part_pos >= y_max_local) ybd = sgn
          END IF
        ELSE
          ! Particle has left this processor
          IF (part_pos >= y_max_local) THEN
            ybd = sgn
            ! Particle has left the system
            IF (y_max_boundary) THEN
              ybd = 0
              bc = bc_species(c_bd_y_max)
              IF (bc == c_bc_reflect) THEN
                ! Approximate reflection as reduction of end-step radius
                radial_reduction = 2.0_num * y_max / part_pos - 1.0_num
                cur%part_pos(2) = cur%part_pos(2) * radial_reduction 
                cur%part_pos(3) = cur%part_pos(3) * radial_reduction
                
                ! Reflect momentum pr to -pr, using end-step theta 
                inv_final_r = 1.0_num / &
                    SQRT(cur%part_pos(2)**2 + cur%part_pos(3)**2)
                cos_theta = cur%part_pos(2) * inv_final_r 
                sin_theta = cur%part_pos(3) * inv_final_r 
                part_pr = cur%part_p(2)*cos_theta + cur%part_p(3)*sin_theta 
                part_pt = -cur%part_p(2)*sin_theta + cur%part_p(3)*cos_theta 
                cur%part_p(2) = -part_pr*cos_theta - part_pt*sin_theta
                cur%part_p(3) = -part_pr*sin_theta + part_pt*cos_theta
              ELSE IF (bc == c_bc_periodic) THEN
                ybd = sgn
                cur%part_pos(2) = part_pos - sgn * y_shift
              END IF
            END IF
            IF (part_pos >= y_max_outer .AND. bc /= c_bc_periodic) THEN
              IF (bc == c_bc_thermal) THEN
                ! Always use the triangle particle weighting for simplicity
                cell_x_r = (cur%part_pos(1) - x_grid_min_local) / dx
                cell_x = FLOOR(cell_x_r + 0.5_num)
                cell_frac_x = REAL(cell_x, num) - cell_x_r
                cell_x = cell_x + 1

                cf2 = cell_frac_x**2
                gx(-1) = 0.5_num * (0.25_num + cf2 + cell_frac_x)
                gx( 0) = 0.75_num - cf2
                gx( 1) = 0.5_num * (0.25_num + cf2 - cell_frac_x)

                DO i = 1, 3
                  temp(i) = 0.0_num
                  DO ix = -1, 1
                    temp(i) = temp(i) + gx(ix) &
                        * species_list(ispecies)%ext_temp_y_max(cell_x+ix, i)
                  END DO
                END DO

                CALL id_registry%delete_all(cur)

                ! x-direction
                i = 1
                cur%part_p(i) = momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num)

                ! y-direction
                i = 2
                cur%part_p(i) = flux_momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num, &
                    -REAL(sgn, num))

                ! z-direction
                i = 3
                cur%part_p(i) = momentum_from_temperature(&
                    species_list(ispecies)%mass, temp(i), 0.0_num)

                cur%part_pos(2) = 2.0_num * y_max_outer - part_pos

              ELSE
                ! Default to open boundary conditions - remove particle
                out_of_bounds = .TRUE.
              END IF
            END IF
          END IF
        END IF

        IF (out_of_bounds) THEN
          ! Particle has gone forever
          CALL remove_particle_from_partlist(&
              species_list(ispecies)%attached_list, cur)
          IF (track_ejected_particles) THEN
            CALL add_particle_to_partlist(&
                ejected_list(ispecies)%attached_list, cur)
          ELSE
            CALL destroy_particle(cur)
          END IF
        ELSE IF (ABS(xbd) + ABS(ybd) > 0) THEN
          ! Particle has left processor, send it to its neighbour
          CALL remove_particle_from_partlist(&
              species_list(ispecies)%attached_list, cur)
          CALL add_particle_to_partlist(send(xbd, ybd), cur)
        END IF

        ! Move to next particle
        cur => next
      END DO

      ! swap Particles
      DO iy = -1, 1
        DO ix = -1, 1
          IF (ABS(ix) + ABS(iy) == 0) CYCLE
          ixp = -ix
          iyp = -iy
          CALL partlist_sendrecv(send(ix, iy), recv(ixp, iyp), &
              neighbour(ix, iy), neighbour(ixp, iyp))
          CALL append_partlist(species_list(ispecies)%attached_list, &
              recv(ixp, iyp))
        END DO
      END DO

      DO iy = -1, 1
        DO ix = -1, 1
          IF (ABS(ix) + ABS(iy) == 0) CYCLE
          CALL destroy_partlist(send(ix, iy))
          CALL destroy_partlist(recv(ix, iy))
        END DO
      END DO

    END DO

  END SUBROUTINE particle_bcs



  SUBROUTINE current_bcs(species)

    INTEGER, INTENT(IN), OPTIONAL :: species
    INTEGER :: im

    ! Domain is decomposed. Just add currents at edges
    DO im = 0,n_mode-1
      CALL processor_summation_bcs_complex(jxm(:,:,im), jng, c_dir_x, species)
      CALL processor_summation_bcs_complex(jrm(:,:,im), jng, c_dir_y, species)
      CALL processor_summation_bcs_complex(jtm(:,:,im), jng, c_dir_z, species)
    END DO

  END SUBROUTINE current_bcs



  SUBROUTINE current_bcs_r_min_final

    ! Sets the value of current densities on and just below the r=0 boundary

    INTEGER :: im, j
    REAL(num) :: mode_sign

    IF (.NOT. y_min_boundary) RETURN 

    mode_sign = 1.0_num
    DO im = 0,n_mode-1

      ! Fold ghost-cells back into simulation window
      DO j=2,jng
        jxm(:,j-1,im) = jxm(:,j-1,im) + mode_sign * jxm(:,1-j,im)
        jrm(:,j  ,im) = jrm(:,j  ,im) - mode_sign * jrm(:,1-j,im)
        jtm(:,j-1,im) = jtm(:,j-1,im) - mode_sign * jtm(:,1-j,im)
        jxm(:,1-j,im) = 0.0_num
        jrm(:,1-j,im) = 0.0_num
        jtm(:,1-j,im) = 0.0_num
      END DO 
      jrm(:,1,im) = jrm(:,1,im) - mode_sign * jrm(:,0,im)

      mode_sign = -mode_sign
    END DO

    DO im = 0, n_mode-1
      ! Jx
      IF (im > 0) THEN
        ! On r=0, jx=0 for m>0 (multi-valued variable forbidden)
        jxm(:,0,im) = 0.0_num
      ELSE 
        ! Force djx/dr = 0 at r=0 for m=0 (jxm is uniform above and below r=0)
        jxm(:,0,im) = (4.0_num * jxm(:,1,im) - jxm(:,2,im)) / 3.0_num
      END IF
    
      ! Jr and Jt 
      IF (im == 1) THEN
        ! Continuity eq. and dJ_perp/dth = 0 on r=0
        jtm(:,0,im) = -imagi * (9.0_num * jrm(:,1,im) - jrm(:,2,im)) / 8.0_num
        ! jrm(r=0) = i*jtm(r=0), from dJ_perp/dth=0 
        jrm(:,0,im) = 2.0_num * imagi * jtm(:,0,im) - jrm(:,1,im)
      ELSE
        ! jtm=0 for m=0 and m>1 (multi-valued variable forbidden)
        jtm(:,0,im) = 0.0_num
        ! Force jrm=0 on r=0 for m=0, m>1
        jrm(:,0,im) = -jrm(:,1,im)
      END IF
    END DO

  END SUBROUTINE current_bcs_r_min_final



  SUBROUTINE set_cpml_helpers(nx, nx_global_min, nx_global_max, &
      ny, ny_global_min, ny_global_max)

    INTEGER, INTENT(IN) :: nx, nx_global_min, nx_global_max
    INTEGER, INTENT(IN) :: ny, ny_global_min, ny_global_max
    INTEGER :: i
    INTEGER :: ix, ix_glob
    INTEGER :: iy, iy_glob
    INTEGER, PARAMETER :: cpml_m = 3
    INTEGER, PARAMETER :: cpml_ma = 1
    REAL(num) :: x_pos, x_pos_m, x_pos_ma
    REAL(num) :: y_pos, y_pos_m, y_pos_ma
    REAL(num) :: cpml_sigma_maxval

    ALLOCATE(cpml_kappa_ex(1-ng:nx+ng), cpml_kappa_bx(1-ng:nx+ng))
    ALLOCATE(cpml_a_ex(1-ng:nx+ng), cpml_a_bx(1-ng:nx+ng))
    ALLOCATE(cpml_sigma_ex(1-ng:nx+ng), cpml_sigma_bx(1-ng:nx+ng))

    ALLOCATE(cpml_kappa_ey(1-ng:ny+ng), cpml_kappa_by(1-ng:ny+ng))
    ALLOCATE(cpml_a_ey(1-ng:ny+ng), cpml_a_by(1-ng:ny+ng))
    ALLOCATE(cpml_sigma_ey(1-ng:ny+ng), cpml_sigma_by(1-ng:ny+ng))

    cpml_kappa_ex = 1.0_num
    cpml_kappa_bx = 1.0_num

    cpml_a_ex = 0.0_num
    cpml_sigma_ex = 0.0_num
    cpml_a_bx = 0.0_num
    cpml_sigma_bx = 0.0_num

    cpml_kappa_ey = 1.0_num
    cpml_kappa_by = 1.0_num

    cpml_a_ey = 0.0_num
    cpml_sigma_ey = 0.0_num
    cpml_a_by = 0.0_num
    cpml_sigma_by = 0.0_num

    cpml_sigma_maxval = cpml_sigma_max * c * 0.8_num * (cpml_m + 1.0_num) / dx

    ! ============= x_min boundary =============

    i = c_bd_x_min
    IF (bc_field(i) == c_bc_cpml_laser &
        .OR. bc_field(i) == c_bc_cpml_outflow) THEN
      cpml_x_min_start = nx+1
      cpml_x_min_end = 0
      cpml_x_min_offset = 0

      IF (nx_global_min <= cpml_thickness) THEN
        cpml_x_min = .TRUE.
        cpml_x_min_start = 1 ! in local grid coordinates

        ! The following distinction is necessary because, in principle, it is
        ! possible for the local domain to lie completely within the boundary
        ! layer.
        IF (nx_global_max >= cpml_thickness) THEN
          ! in local grid coordinates
          ! global -> local: ixl = ixg - nx_global_min + 1
          cpml_x_min_end = cpml_thickness - nx_global_min + 1
          cpml_x_min_offset = cpml_thickness - nx_global_min + 1
        ELSE
          cpml_x_min_end = nx ! in local grid coordinates
          cpml_x_min_offset = cpml_thickness
        END IF

        DO ix = cpml_x_min_start,cpml_x_min_end
          ! runs from 1 to cpml_thickness in global coordinates
          ! local -> global: ixg = ixl + nx_global_min - 1
          ix_glob = ix + nx_global_min - 1

          ! runs from 1.0 to nearly 0.0 (actually 0.0 at cpml_thickness+1)
          x_pos = 1.0_num - REAL(ix_glob-1,num) / REAL(cpml_thickness,num)
          x_pos_m = x_pos**cpml_m
          x_pos_ma = (1.0_num - x_pos)**cpml_ma

          cpml_kappa_ex(ix) = 1.0_num + (cpml_kappa_max - 1.0_num) * x_pos_m
          cpml_sigma_ex(ix) = cpml_sigma_maxval * x_pos_m
          cpml_a_ex(ix) = cpml_a_max * x_pos_ma

          ! runs from nearly 1.0 to nearly 0.0 on the half intervals
          ! 1.0 at ix_glob=1-1/2 and 0.0 at ix_glob=cpml_thickness+1/2
          x_pos = 1.0_num - (REAL(ix_glob,num) - 0.5_num) &
              / REAL(cpml_thickness,num)
          x_pos_m = x_pos**cpml_m
          x_pos_ma = (1.0_num - x_pos)**cpml_ma

          cpml_kappa_bx(ix) = 1.0_num + (cpml_kappa_max - 1.0_num) * x_pos_m
          cpml_sigma_bx(ix) = cpml_sigma_maxval * x_pos_m
          cpml_a_bx(ix) = cpml_a_max * x_pos_ma
        END DO
      END IF

      ! Ghost cells start at the edge of the CPML boundary
      IF (nx_global_min <= cpml_thickness + fng + 1 &
          .AND. nx_global_max >= cpml_thickness + fng + 1) THEN
        add_laser(i) = .TRUE.
        cpml_x_min_laser_idx = cpml_thickness + fng + 1 - nx_global_min
      END IF
    END IF

    ! ============= x_max boundary =============

    i = c_bd_x_max
    ! Same as x_min using the transformation ix -> nx_global - ix + 1
    IF (bc_field(i) == c_bc_cpml_laser &
        .OR. bc_field(i) == c_bc_cpml_outflow) THEN
      cpml_x_max_start = nx+1
      cpml_x_max_end = 0
      cpml_x_max_offset = 0

      IF (nx_global_max >= nx_global - cpml_thickness + 1) THEN
        cpml_x_max = .TRUE.
        cpml_x_max_end = nx ! in local grid coordinates

        ! The following distinction is necessary because, in principle, it is
        ! possible for the local domain to lie completely within the boundary
        ! layer.
        IF (nx_global_min <= nx_global - cpml_thickness + 1) THEN
          ! in local grid coordinates
          ! global -> local: ixl = ixg - nx_global_min + 1
          cpml_x_max_start = nx_global - cpml_thickness + 1 - nx_global_min + 1
          cpml_x_max_offset = cpml_thickness - nx_global + nx_global_max
        ELSE
          cpml_x_max_start = 1 ! in local grid coordinates
          cpml_x_max_offset = cpml_thickness
        END IF

        DO ix = cpml_x_max_start,cpml_x_max_end
          ! runs from cpml_thickness to 1 in global coordinates
          ! local -> global: ixg = ixl + nx_global_min - 1
          ix_glob = nx_global - (ix + nx_global_min - 1) + 1

          ! runs from nearly 0.0 (actually 0.0 at cpml_thickness+1) to 1.0
          x_pos = 1.0_num - REAL(ix_glob-1,num) / REAL(cpml_thickness,num)
          x_pos_m = x_pos**cpml_m
          x_pos_ma = (1.0_num - x_pos)**cpml_ma

          cpml_kappa_ex(ix) = 1.0_num + (cpml_kappa_max - 1.0_num) * x_pos_m
          cpml_sigma_ex(ix) = cpml_sigma_maxval * x_pos_m
          cpml_a_ex(ix) = cpml_a_max * x_pos_ma

          ! runs from nearly 0.0 to nearly 1.0 on the half intervals
          ! 0.0 at ix_glob=cpml_thickness+1/2 and 1.0 at ix_glob=1-1/2
          x_pos = 1.0_num - (REAL(ix_glob,num) - 0.5_num) &
              / REAL(cpml_thickness,num)
          x_pos_m = x_pos**cpml_m
          x_pos_ma = (1.0_num - x_pos)**cpml_ma

          cpml_kappa_bx(ix-1) = 1.0_num + (cpml_kappa_max - 1.0_num) * x_pos_m
          cpml_sigma_bx(ix-1) = cpml_sigma_maxval * x_pos_m
          cpml_a_bx(ix-1) = cpml_a_max * x_pos_ma
        END DO
      END IF

      ! Ghost cells start at the edge of the CPML boundary
      IF (nx_global_min <= nx_global - cpml_thickness - fng + 2 &
          .AND. nx_global_max >= nx_global - cpml_thickness - fng + 2) THEN
        add_laser(i) = .TRUE.
        cpml_x_max_laser_idx = &
            nx_global - cpml_thickness - fng + 2 - nx_global_min
      END IF
    END IF

    ! ============= y_min boundary =============

    i = c_bd_y_min
    IF (bc_field(i) == c_bc_cpml_laser &
        .OR. bc_field(i) == c_bc_cpml_outflow) THEN
      cpml_y_min_start = ny+1
      cpml_y_min_end = 0
      cpml_y_min_offset = 0

      IF (ny_global_min <= cpml_thickness) THEN
        cpml_y_min = .TRUE.
        cpml_y_min_start = 1 ! in local grid coordinates

        ! The following distinction is necessary because, in principle, it is
        ! possible for the local domain to lie completely within the boundary
        ! layer.
        IF (ny_global_max >= cpml_thickness) THEN
          ! in local grid coordinates
          ! global -> local: iyl = iyg - ny_global_min + 1
          cpml_y_min_end = cpml_thickness - ny_global_min + 1
          cpml_y_min_offset = cpml_thickness - ny_global_min + 1
        ELSE
          cpml_y_min_end = ny ! in local grid coordinates
          cpml_y_min_offset = cpml_thickness
        END IF

        DO iy = cpml_y_min_start,cpml_y_min_end
          ! runs from 1 to cpml_thickness in global coordinates
          ! local -> global: iyg = iyl + ny_global_min - 1
          iy_glob = iy + ny_global_min - 1

          ! runs from 1.0 to nearly 0.0 (actually 0.0 at cpml_thickness+1)
          y_pos = 1.0_num - REAL(iy_glob-1,num) / REAL(cpml_thickness,num)
          y_pos_m = y_pos**cpml_m
          y_pos_ma = (1.0_num - y_pos)**cpml_ma

          cpml_kappa_ey(iy) = 1.0_num + (cpml_kappa_max - 1.0_num) * y_pos_m
          cpml_sigma_ey(iy) = cpml_sigma_maxval * y_pos_m
          cpml_a_ey(iy) = cpml_a_max * y_pos_ma

          ! runs from nearly 1.0 to nearly 0.0 on the half intervals
          ! 1.0 at iy_glob=1-1/2 and 0.0 at iy_glob=cpml_thickness+1/2
          y_pos = 1.0_num - (REAL(iy_glob,num) - 0.5_num) &
              / REAL(cpml_thickness,num)
          y_pos_m = y_pos**cpml_m
          y_pos_ma = (1.0_num - y_pos)**cpml_ma

          cpml_kappa_by(iy) = 1.0_num + (cpml_kappa_max - 1.0_num) * y_pos_m
          cpml_sigma_by(iy) = cpml_sigma_maxval * y_pos_m
          cpml_a_by(iy) = cpml_a_max * y_pos_ma
        END DO
      END IF

      ! Ghost cells start at the edge of the CPML boundary
      IF (ny_global_min <= cpml_thickness + fng + 1 &
          .AND. ny_global_max >= cpml_thickness + fng + 1) THEN
        add_laser(i) = .TRUE.
        cpml_y_min_laser_idx = cpml_thickness + fng + 1 - ny_global_min
      END IF
    END IF

    ! ============= y_max boundary =============

    i = c_bd_y_max
    ! Same as y_min using the transformation iy -> ny_global - iy + 1
    IF (bc_field(i) == c_bc_cpml_laser &
        .OR. bc_field(i) == c_bc_cpml_outflow) THEN
      cpml_y_max_start = ny+1
      cpml_y_max_end = 0
      cpml_y_max_offset = 0

      IF (ny_global_max >= ny_global - cpml_thickness + 1) THEN
        cpml_y_max = .TRUE.
        cpml_y_max_end = ny ! in local grid coordinates

        ! The following distinction is necessary because, in principle, it is
        ! possible for the local domain to lie completely within the boundary
        ! layer.
        IF (ny_global_min <= ny_global - cpml_thickness + 1) THEN
          ! in local grid coordinates
          ! global -> local: iyl = iyg - ny_global_min + 1
          cpml_y_max_start = ny_global - cpml_thickness + 1 - ny_global_min + 1
          cpml_y_max_offset = cpml_thickness - ny_global + ny_global_max
        ELSE
          cpml_y_max_start = 1 ! in local grid coordinates
          cpml_y_max_offset = cpml_thickness
        END IF

        DO iy = cpml_y_max_start,cpml_y_max_end
          ! runs from cpml_thickness to 1 in global coordinates
          ! local -> global: iyg = iyl + ny_global_min - 1
          iy_glob = ny_global - (iy + ny_global_min - 1) + 1

          ! runs from nearly 0.0 (actually 0.0 at cpml_thickness+1) to 1.0
          y_pos = 1.0_num - REAL(iy_glob-1,num) / REAL(cpml_thickness,num)
          y_pos_m = y_pos**cpml_m
          y_pos_ma = (1.0_num - y_pos)**cpml_ma

          cpml_kappa_ey(iy) = 1.0_num + (cpml_kappa_max - 1.0_num) * y_pos_m
          cpml_sigma_ey(iy) = cpml_sigma_maxval * y_pos_m
          cpml_a_ey(iy) = cpml_a_max * y_pos_ma

          ! runs from nearly 0.0 to nearly 1.0 on the half intervals
          ! 0.0 at iy_glob=cpml_thickness+1/2 and 1.0 at iy_glob=1-1/2
          y_pos = 1.0_num - (REAL(iy_glob,num) - 0.5_num) &
              / REAL(cpml_thickness,num)
          y_pos_m = y_pos**cpml_m
          y_pos_ma = (1.0_num - y_pos)**cpml_ma

          cpml_kappa_by(iy-1) = 1.0_num + (cpml_kappa_max - 1.0_num) * y_pos_m
          cpml_sigma_by(iy-1) = cpml_sigma_maxval * y_pos_m
          cpml_a_by(iy-1) = cpml_a_max * y_pos_ma
        END DO
      END IF

      ! Ghost cells start at the edge of the CPML boundary
      IF (ny_global_min <= ny_global - cpml_thickness - fng + 2 &
          .AND. ny_global_max >= ny_global - cpml_thickness - fng + 2) THEN
        add_laser(i) = .TRUE.
        cpml_y_max_laser_idx = &
            ny_global - cpml_thickness - fng + 2 - ny_global_min
      END IF
    END IF

    x_min_local = x_grid_min_local + (cpml_x_min_offset - 0.5_num) * dx
    x_max_local = x_grid_max_local - (cpml_x_max_offset - 0.5_num) * dx
    y_min_local = y_grid_min_local + (cpml_y_min_offset - 0.5_num) * dy
    y_max_local = y_grid_max_local - (cpml_y_max_offset - 0.5_num) * dy

  END SUBROUTINE set_cpml_helpers



  SUBROUTINE allocate_cpml_fields

    ! I will ignore memory consumption issues and, for simplicity,
    ! allocate the boundary fields throughout the whole simulation box.
    ALLOCATE(cpml_psi_eyx(1-ng:nx+ng,1-ng:ny+ng))
    ALLOCATE(cpml_psi_ezx(1-ng:nx+ng,1-ng:ny+ng))
    ALLOCATE(cpml_psi_byx(1-ng:nx+ng,1-ng:ny+ng))
    ALLOCATE(cpml_psi_bzx(1-ng:nx+ng,1-ng:ny+ng))

    ALLOCATE(cpml_psi_exy(1-ng:nx+ng,1-ng:ny+ng))
    ALLOCATE(cpml_psi_ezy(1-ng:nx+ng,1-ng:ny+ng))
    ALLOCATE(cpml_psi_bxy(1-ng:nx+ng,1-ng:ny+ng))
    ALLOCATE(cpml_psi_bzy(1-ng:nx+ng,1-ng:ny+ng))

    cpml_psi_eyx = 0.0_num
    cpml_psi_ezx = 0.0_num
    cpml_psi_byx = 0.0_num
    cpml_psi_bzx = 0.0_num

    cpml_psi_exy = 0.0_num
    cpml_psi_ezy = 0.0_num
    cpml_psi_bxy = 0.0_num
    cpml_psi_bzy = 0.0_num

  END SUBROUTINE allocate_cpml_fields



  SUBROUTINE deallocate_cpml_helpers

    DEALLOCATE(cpml_kappa_ex, cpml_kappa_bx)
    DEALLOCATE(cpml_a_ex, cpml_a_bx)
    DEALLOCATE(cpml_sigma_ex, cpml_sigma_bx)

    DEALLOCATE(cpml_kappa_ey, cpml_kappa_by)
    DEALLOCATE(cpml_a_ey, cpml_a_by)
    DEALLOCATE(cpml_sigma_ey, cpml_sigma_by)

  END SUBROUTINE deallocate_cpml_helpers



  SUBROUTINE cpml_advance_e_currents(tstep)

    REAL(num), INTENT(IN) :: tstep
    INTEGER :: ipos, ix, iy
    REAL(num) :: acoeff, bcoeff, ccoeff_d, fac
    REAL(num) :: kappa, sigma

    fac = tstep * c**2

    ! ============= x_min boundary =============

    IF (bc_field(c_bd_x_min) == c_bc_cpml_laser &
        .OR. bc_field(c_bd_x_min) == c_bc_cpml_outflow) THEN
      DO iy = 1,ny
        DO ipos = cpml_x_min_start,cpml_x_min_end
          kappa = cpml_kappa_ex(ipos)
          sigma = cpml_sigma_ex(ipos)
          acoeff = cpml_a_ex(ipos)
          bcoeff = EXP(-(sigma / kappa + acoeff) * tstep)
          ccoeff_d = (bcoeff - 1.0_num) * sigma / kappa &
              / (sigma + kappa * acoeff) / dx

          cpml_psi_eyx(ipos,iy) = bcoeff * cpml_psi_eyx(ipos,iy) &
              + ccoeff_d * (bz(ipos,iy) - bz(ipos-1,iy))
          ey(ipos,iy) = ey(ipos,iy) - fac * cpml_psi_eyx(ipos,iy)

          cpml_psi_ezx(ipos,iy) = bcoeff * cpml_psi_ezx(ipos,iy) &
              + ccoeff_d * (by(ipos,iy) - by(ipos-1,iy))
          ez(ipos,iy) = ez(ipos,iy) + fac * cpml_psi_ezx(ipos,iy)
        END DO
      END DO
    END IF

    ! ============= x_max boundary =============

    IF (bc_field(c_bd_x_max) == c_bc_cpml_laser &
        .OR. bc_field(c_bd_x_max) == c_bc_cpml_outflow) THEN
      DO iy = 1,ny
        DO ipos = cpml_x_max_start,cpml_x_max_end
          kappa = cpml_kappa_ex(ipos)
          sigma = cpml_sigma_ex(ipos)
          acoeff = cpml_a_ex(ipos)
          bcoeff = EXP(-(sigma / kappa + acoeff) * tstep)
          ccoeff_d = (bcoeff - 1.0_num) * sigma / kappa &
              / (sigma + kappa * acoeff) / dx

          cpml_psi_eyx(ipos,iy) = bcoeff * cpml_psi_eyx(ipos,iy) &
              + ccoeff_d * (bz(ipos,iy) - bz(ipos-1,iy))
          ey(ipos,iy) = ey(ipos,iy) - fac * cpml_psi_eyx(ipos,iy)

          cpml_psi_ezx(ipos,iy) = bcoeff * cpml_psi_ezx(ipos,iy) &
              + ccoeff_d * (by(ipos,iy) - by(ipos-1,iy))
          ez(ipos,iy) = ez(ipos,iy) + fac * cpml_psi_ezx(ipos,iy)
        END DO
      END DO
    END IF

    ! ============= y_min boundary =============

    IF (bc_field(c_bd_y_min) == c_bc_cpml_laser &
        .OR. bc_field(c_bd_y_min) == c_bc_cpml_outflow) THEN
      DO ipos = cpml_y_min_start,cpml_y_min_end
        kappa = cpml_kappa_ey(ipos)
        sigma = cpml_sigma_ey(ipos)
        acoeff = cpml_a_ey(ipos)
        bcoeff = EXP(-(sigma / kappa + acoeff) * tstep)
        ccoeff_d = (bcoeff - 1.0_num) * sigma / kappa &
            / (sigma + kappa * acoeff) / dy

        DO ix = 1,nx
          cpml_psi_exy(ix,ipos) = bcoeff * cpml_psi_exy(ix,ipos) &
              + ccoeff_d * (bz(ix,ipos) - bz(ix,ipos-1))
          ex(ix,ipos) = ex(ix,ipos) + fac * cpml_psi_exy(ix,ipos)

          cpml_psi_ezy(ix,ipos) = bcoeff * cpml_psi_ezy(ix,ipos) &
              + ccoeff_d * (bx(ix,ipos) - bx(ix,ipos-1))
          ez(ix,ipos) = ez(ix,ipos) - fac * cpml_psi_ezy(ix,ipos)
        END DO
      END DO
    END IF

    ! ============= y_max boundary =============

    IF (bc_field(c_bd_y_max) == c_bc_cpml_laser &
        .OR. bc_field(c_bd_y_max) == c_bc_cpml_outflow) THEN
      DO ipos = cpml_y_max_start,cpml_y_max_end
        kappa = cpml_kappa_ey(ipos)
        sigma = cpml_sigma_ey(ipos)
        acoeff = cpml_a_ey(ipos)
        bcoeff = EXP(-(sigma / kappa + acoeff) * tstep)
        ccoeff_d = (bcoeff - 1.0_num) * sigma / kappa &
            / (sigma + kappa * acoeff) / dy

        DO ix = 1,nx
          cpml_psi_exy(ix,ipos) = bcoeff * cpml_psi_exy(ix,ipos) &
              + ccoeff_d * (bz(ix,ipos) - bz(ix,ipos-1))
          ex(ix,ipos) = ex(ix,ipos) + fac * cpml_psi_exy(ix,ipos)

          cpml_psi_ezy(ix,ipos) = bcoeff * cpml_psi_ezy(ix,ipos) &
              + ccoeff_d * (bx(ix,ipos) - bx(ix,ipos-1))
          ez(ix,ipos) = ez(ix,ipos) - fac * cpml_psi_ezy(ix,ipos)
        END DO
      END DO
    END IF

  END SUBROUTINE cpml_advance_e_currents



  SUBROUTINE cpml_advance_b_currents(tstep)

    REAL(num), INTENT(IN) :: tstep
    INTEGER :: ipos, ix, iy
    REAL(num) :: acoeff, bcoeff, ccoeff_d
    REAL(num) :: kappa, sigma

    ! ============= x_min boundary =============

    IF (bc_field(c_bd_x_min) == c_bc_cpml_laser &
        .OR. bc_field(c_bd_x_min) == c_bc_cpml_outflow) THEN
      DO iy = 1,ny
        DO ipos = cpml_x_min_start,cpml_x_min_end
          kappa = cpml_kappa_bx(ipos)
          sigma = cpml_sigma_bx(ipos)
          acoeff = cpml_a_bx(ipos)
          bcoeff = EXP(-(sigma / kappa + acoeff) * tstep)
          ccoeff_d = (bcoeff - 1.0_num) * sigma / kappa &
              / (sigma + kappa * acoeff) / dx

          cpml_psi_byx(ipos,iy) = bcoeff * cpml_psi_byx(ipos,iy) &
              + ccoeff_d * (ez(ipos+1,iy) - ez(ipos,iy))
          by(ipos,iy) = by(ipos,iy) + tstep * cpml_psi_byx(ipos,iy)

          cpml_psi_bzx(ipos,iy) = bcoeff * cpml_psi_bzx(ipos,iy) &
              + ccoeff_d * (ey(ipos+1,iy) - ey(ipos,iy))
          bz(ipos,iy) = bz(ipos,iy) - tstep * cpml_psi_bzx(ipos,iy)
        END DO
      END DO
    END IF

    ! ============= x_max boundary =============

    IF (bc_field(c_bd_x_max) == c_bc_cpml_laser &
        .OR. bc_field(c_bd_x_max) == c_bc_cpml_outflow) THEN
      DO iy = 1,ny
        DO ipos = cpml_x_max_start-1,cpml_x_max_end-1
          kappa = cpml_kappa_bx(ipos)
          sigma = cpml_sigma_bx(ipos)
          acoeff = cpml_a_bx(ipos)
          bcoeff = EXP(-(sigma / kappa + acoeff) * tstep)
          ccoeff_d = (bcoeff - 1.0_num) * sigma / kappa &
              / (sigma + kappa * acoeff) / dx

          cpml_psi_byx(ipos,iy) = bcoeff * cpml_psi_byx(ipos,iy) &
              + ccoeff_d * (ez(ipos+1,iy) - ez(ipos,iy))
          by(ipos,iy) = by(ipos,iy) + tstep * cpml_psi_byx(ipos,iy)

          cpml_psi_bzx(ipos,iy) = bcoeff * cpml_psi_bzx(ipos,iy) &
              + ccoeff_d * (ey(ipos+1,iy) - ey(ipos,iy))
          bz(ipos,iy) = bz(ipos,iy) - tstep * cpml_psi_bzx(ipos,iy)
        END DO
      END DO
    END IF

    ! ============= y_min boundary =============

    IF (bc_field(c_bd_y_min) == c_bc_cpml_laser &
        .OR. bc_field(c_bd_y_min) == c_bc_cpml_outflow) THEN
      DO ipos = cpml_y_min_start,cpml_y_min_end
        kappa = cpml_kappa_by(ipos)
        sigma = cpml_sigma_by(ipos)
        acoeff = cpml_a_by(ipos)
        bcoeff = EXP(-(sigma / kappa + acoeff) * tstep)
        ccoeff_d = (bcoeff - 1.0_num) * sigma / kappa &
            / (sigma + kappa * acoeff) / dy

        DO ix = 1,nx
          cpml_psi_bxy(ix,ipos) = bcoeff * cpml_psi_bxy(ix,ipos) &
              + ccoeff_d * (ez(ix,ipos+1) - ez(ix,ipos))
          bx(ix,ipos) = bx(ix,ipos) - tstep * cpml_psi_bxy(ix,ipos)

          cpml_psi_bzy(ix,ipos) = bcoeff * cpml_psi_bzy(ix,ipos) &
              + ccoeff_d * (ex(ix,ipos+1) - ex(ix,ipos))
          bz(ix,ipos) = bz(ix,ipos) + tstep * cpml_psi_bzy(ix,ipos)
        END DO
      END DO
    END IF

    ! ============= y_max boundary =============

    IF (bc_field(c_bd_y_max) == c_bc_cpml_laser &
        .OR. bc_field(c_bd_y_max) == c_bc_cpml_outflow) THEN
      DO ipos = cpml_y_max_start-1,cpml_y_max_end-1
        kappa = cpml_kappa_by(ipos)
        sigma = cpml_sigma_by(ipos)
        acoeff = cpml_a_by(ipos)
        bcoeff = EXP(-(sigma / kappa + acoeff) * tstep)
        ccoeff_d = (bcoeff - 1.0_num) * sigma / kappa &
            / (sigma + kappa * acoeff) / dy

        DO ix = 1,nx
          cpml_psi_bxy(ix,ipos) = bcoeff * cpml_psi_bxy(ix,ipos) &
              + ccoeff_d * (ez(ix,ipos+1) - ez(ix,ipos))
          bx(ix,ipos) = bx(ix,ipos) - tstep * cpml_psi_bxy(ix,ipos)

          cpml_psi_bzy(ix,ipos) = bcoeff * cpml_psi_bzy(ix,ipos) &
              + ccoeff_d * (ex(ix,ipos+1) - ex(ix,ipos))
          bz(ix,ipos) = bz(ix,ipos) + tstep * cpml_psi_bzy(ix,ipos)
        END DO
      END DO
    END IF

  END SUBROUTINE cpml_advance_b_currents

END MODULE boundary
